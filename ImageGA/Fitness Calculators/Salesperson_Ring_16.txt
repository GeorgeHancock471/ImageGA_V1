setBatchMode("true");


//__________________________________________________________________

//=========================================================
/*
FITNESS PRE-CHECKER (Recommended for all calculators )
_____________________________________________________

Counts the number of survival tables in the last directory used by ImageJ

 NOTE: the algorithim will always premake a results table.


*/
//=========================================================



settingsFilePath = getDirectory("plugins") + "ImageGA/RequisitFunctions/PopulationLocation.txt";


if(File.exists(settingsFilePath) == 1){
	settingsString=File.openAsString(settingsFilePath);	// open txt data file
	defaultSettings=split(settingsString, "\n");
} 


//print(defaultSettings[0]);

CheckDirectory = defaultSettings[0];

GeneratArray = newArray("none");

GeneratList  =  getFileList(CheckDirectory);

//Array.show(GeneratList);

for(i=0; i<GeneratList.length; i++) { // list all .txt files

	if(endsWith(GeneratList[i], "_survival.txt")==1){
		GeneratArray = Array.concat(GeneratArray, GeneratList[i]);
	}


}


ArrayLeng = lengthOf(GeneratArray);

//print (ArrayLeng);

//Array.show(GeneratArray);


csv = ".csv";
tif = ".tif";
txt = ".txt";
png = ".png";


// Index
//---------------------------------------------------
// Allows you to obtain the index of a Value within an Array.

  function index(a, value) {
      for (i=0; i<a.length; i++)
          if (a[i]==value) return i;
      return -1;
  }

// Obtain SubArray
//---------------------------------------------------
// Allows you to obtain a SubArray of a given Name and Length, where a is the array and b is the original number of rows.

 function subArray(a, value, b) {
	IndexLab = index(a, value);
	Start = IndexLab + 1;
	End = IndexLab  + b;
	//print("startIndex: "+Start);
	//print("endIndex: "+End);
	return Array.slice(a, Start, End);
}


//--------------------------------------------------------------------------------------------

GenNumber = ArrayLeng -1;
GenNum = CheckDirectory + "generation_" + GenNumber + txt;
GenName = "generation_" + GenNumber + txt;
SurvGenNum= CheckDirectory + "generation_" + GenNumber +"_survival" + txt;

//------------------------------------
//Load Current Generation
//------------------------------------

if(File.exists(GenNum) == 1){
GenerationString=File.openAsString(GenNum);	// open txt data file
GenerationRows=split(GenerationString, "\n");

	Column_Titles = GenerationRows[0];
	CT=split(Column_Titles, "\t");
	SiteNames=Array.deleteValue(CT, "ID");
	SiteNames=Array.deleteValue(SiteNames, "Parent1");
	SiteNames=Array.deleteValue(SiteNames, "Parent2");
	F = lengthOf(CT);
	RouteLeng = F;
	
VarLeng = GenerationRows.length-1;
ColumnLength = VarLeng;

Genotype_IDs = newArray();
Col1 = newArray();
for(X=1; X<GenerationRows.length; X++){
Temp = split(GenerationRows[X], "\t");
Genotype_IDs = Array.concat(Genotype_IDs, Temp);
Col1 = Array.concat(Col1, Temp[0]);
}
//Array.show(Genotype_IDs, Col1);





//------------------------------------------------
// Make Pattern Directory
//-------------------------------------------------
fold = "SalespersonRoutes";
PatternPath = CheckDirectory + fold;
File.makeDirectory(PatternPath );


noiseAmount = parseFloat(File.openAsString( getDirectory("plugins") + "ImageGA/Fitness Calculators/Salesperson/noise.txt" ) );

	//Open Map
//===========================

photoFilePath = getDirectory("plugins") + "ImageGA/Fitness Calculators/Salesperson/Ring.jpg";
open(photoFilePath);
rename("Map.jpg");


	//Set Sites
//============================

City_1 = newArray("City_1", 326, 74);

City_2 = newArray("City_2", 397, 137);

City_3 = newArray("City_3",500, 150);

City_4 = newArray("City_4", 554, 224);

City_5 = newArray("City_5", 569, 293);

City_6 = newArray("City_6", 538, 372);

City_7 = newArray ("City_7", 517, 476);

City_8 = newArray("City_8", 424, 562);

City_9 = newArray("City_9", 324, 564);

City_10 = newArray("City_10", 254, 510);

City_11 = newArray("City_11", 152, 493);

City_12 = newArray("City_12", 97, 398);

City_13 = newArray("City_13",80, 296);

City_14 = newArray("City_14", 152, 213);

City_15= newArray("City_15", 170, 128);

City_16 = newArray("City_16", 246, 106);


// Create Array of All Sites
///===============================
SitesArray = Array.concat(City_1,City_2,City_3,City_4,City_5,City_6,City_7,City_8,City_9,City_10,City_11,City_12,City_13,City_14,City_15,City_16);




//		Get Site Order
//================================
FitnessArray=newArray(ColumnLength);


for ( k=0; k<ColumnLength; k++ ) { 

SiteOrderArray = subArray(Genotype_IDs, Col1[k], F-2);

SiteOrderArray2 = Array.rankPositions(SiteOrderArray);
SiteOrderArray2  = Array.concat(SiteOrderArray2, SiteOrderArray2[0]);
SiteOrderArray3=newArray();

for (i=0; i<SiteOrderArray2.length; i++){
Temp=SiteOrderArray2[i];
Temp=SiteNames[Temp];
SiteOrderArray3=Array.concat(SiteOrderArray3,Temp);
}
//Array.show(SiteOrderArray2, SiteOrderArray3);

//print(VarLeng);


//	Get Fitness
//===============================

RouteLength = 0;

	//	RouteLength
	//------------------------------------------------------

	for (f=0; f<SiteOrderArray2.length-1; f++){

	Site1a = SiteOrderArray2[f];
	Site2a = SiteOrderArray2[f+1];

	Site1b = SiteNames[Site1a];
	Site2b = SiteNames[Site2a];
	
	
	Site1 = subArray(SitesArray, Site1b, 3);
	Site2 =  subArray(SitesArray, Site2b, 3);
	


	route = Array.concat(Site1,Site2);
	
	
	
	a1 =  Math.sqr(parseFloat(route[0]) - parseFloat(route[2]) );

	b1 =  Math.sqr(parseFloat(route[1]) - parseFloat(route[3]) );

	Length = Math.sqrt (  a1 + b1 );  



	RouteLength = RouteLength + Length;
	

	
			}


	
	RouteSpeed = -1 * RouteLength;	

	FitnessArray[k] = RouteSpeed;
	

	}
	


IDArray = Array.copy(Col1);

surivalString1 = Array.concat("ID", "fitness");
surivalString = String.join(surivalString1, "\t");
File.append(surivalString, SurvGenNum);

for(i=0; i< IDArray.length; i++){

surivalString1 = Array.concat(IDArray[i], FitnessArray[i]);

surivalString = String.join(surivalString1, "\t");

File.append(surivalString, SurvGenNum);

}

//File.close(dataFile);




//PRINT The BEST Salesperson
//-----------------------------------------------------

setColor("red");
setLineWidth(4);

//Get the best Route

FitnessRanking = Array.rankPositions(FitnessArray);

Fittest = lengthOf(FitnessRanking)-1;

FittestID = FitnessRanking[Fittest];

FittestID = IDArray[FittestID];

k = FittestID;

SiteOrderArray = subArray(Genotype_IDs, FittestID, F-2);

SiteOrderArray2 = Array.rankPositions(SiteOrderArray);
SiteOrderArray2 = Array.concat(SiteOrderArray2, SiteOrderArray2[0]);


//	Get Fitness
//===============================

	//	RouteLength
	//------------------------------------------------------

	for (f=0; f<SiteOrderArray2.length-1; f++){
	
	label="route_"+f;

	Site1a = SiteOrderArray2[f];

	Site2a = SiteOrderArray2[f+1];

	Site1b = SiteNames[Site1a];

	Site2b = SiteNames[Site2a];
	
	
	Site1 = subArray(SitesArray, Site1b, 3);

	Site2 =  subArray(SitesArray, Site2b, 3);
	
	route = Array.concat(Site1,Site2);

	makeLine(route[0],route[1],route[2],route[3],3);

	roiManager("Add");
	roiManager("select", roiManager("count")-1  );
	roiManager("rename", label);


	}


	roiManager("Select", Array.getSequence(roiManager("count")));
	roiManager("XOR");
	roiManager("Add");
	roiManager("select", roiManager("count")-1  );
	roiManager("rename", "Full Path");


selectWindow("Map.jpg");
roiManager("select", roiManager("count")-1  );
run("Fill", "slice");
pat = "\\" + "Fittest Route_Gen" + GenNumber + tif;
PatternPath = CheckDirectory + fold + pat;
saveAs("Tiff", PatternPath);

close("*");

roiManager("Deselect");

roiManager("Delete");





}


setBatchMode("false");
